plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "net.ltgt.apt-idea" version "0.18"
    id 'com.google.protobuf' version '0.8.10'
    id "io.freefair.lombok" version "4.1.5"
    id "application"
    id "java"
}

version "0.1"
group "com.metapack.bowling"

repositories {
    mavenLocal()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.3.0.M1'
    }
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly 'org.projectlombok:lombok:1.18.10'
    compileOnly "io.micronaut:micronaut-validation"

    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut.grpc:micronaut-grpc-runtime"
    implementation 'io.envoyproxy.protoc-gen-validate:pgv-java-grpc:0.2.0'

    runtimeOnly "io.micronaut:micronaut-runtime"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testCompileOnly "io.micronaut:micronaut-inject-java"
    testImplementation 'io.micronaut.test:micronaut-test-junit5'
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.3.0"
    testImplementation "org.mockito:mockito-junit-jupiter:2.22.0"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.0"
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "com.metapack.bowling.Application"
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

ext {
    protocVersion="3.5.1"
    grpcVersion="1.17.1"
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        javapgv { artifact = "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:0.2.0" }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
            javapgv {
                option "lang=java"
            }
        }
    }
}
